plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'maven-publish'
}

group = 'com.cardanoj'
version = '0.0.8-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.3.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar() // Generates sources JAR
}

tasks.withType(Test) {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'cardanoj-api'
            from components.java

            pom {
                name.set('CardanoJ API')
                description.set('It simplifies the process for Java developers to integrate Cardano functionalities into their applications.')
                url.set('https://github.com/CardanoJ/cardanoJ')
                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0')
                    }
                }
                developers {
                    developer {
                        id.set('Quotus-dev')
                        name.set('Anjali Sethy')
                        email.set('anjalisethy1234@gmail.com')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/CardanoJ/cardanoJ.git')
                    developerConnection.set('scm:git:ssh://github.com/CardanoJ/cardanoJ.git')
                    url.set('https://github.com/CardanoJ/cardanoJ')
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/CardanoJ/cardanoJ')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = project.findProperty('gpr.token') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}


wrapper {
    gradleVersion = '7.6'
    distributionType = Wrapper.DistributionType.ALL
}
